@echo off

REM ---------------------------------------------------------------------------
REM        Copyright 2005-2009 WSO2, Inc. http://www.wso2.org
REM
REM  Licensed under the Apache License, Version 2.0 (the "License");
REM  you may not use this file except in compliance with the License.
REM  You may obtain a copy of the License at
REM
REM      http://www.apache.org/licenses/LICENSE-2.0
REM
REM  Unless required by applicable law or agreed to in writing, software
REM  distributed under the License is distributed on an "AS IS" BASIS,
REM  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
REM  See the License for the specific language governing permissions and
REM  limitations under the License.

rem ---------------------------------------------------------------------------
rem Script for WSO2 Governance Checkin-Client
rem
rem Environment Variable Prequisites
rem
rem   CARBON_HOME   Home of CARBON installation. If not set I will  try
rem                   to figure it out.
rem
rem   JAVA_HOME       Must point at your Java Development Kit installation.
rem
rem   JAVA_OPTS       (Optional) Java runtime options used when the commands
rem                   is executed.
rem ---------------------------------------------------------------------------

rem ----- if JAVA_HOME is not set we're not happy ------------------------------
:checkJava

if "%JAVA_HOME%" == "" goto noJavaHome
if not exist "%JAVA_HOME%\bin\java.exe" goto noJavaHome
goto checkServer

:noJavaHome
echo "You must set the JAVA_HOME variable before running CARBON."
goto end


rem ----- Only set CARBON_HOME if not already set ----------------------------
:checkServer
rem %~sdp0 is expanded pathname of the current script under NT with spaces in the path removed
if "%CARBON_HOME%"=="" set CARBON_HOME=%~sdp0..
SET curDrive=%cd:~0,1%
SET wsasDrive=%CARBON_HOME:~0,1%
if not "%curDrive%" == "%wsasDrive%" %wsasDrive%:

rem find CARBON_HOME if it does not exist due to either an invalid value passed
rem by the user or the %0 problem on Windows 9x
if not exist "%CARBON_HOME%\bin\version.txt" goto noServerHome

set AXIS2_HOME=%CARBON_HOME%
goto updateClasspath

:noServerHome
echo CARBON_HOME is set incorrectly or CARBON could not be located. Please set CARBON_HOME.
goto end


rem ----- update classpath -----------------------------------------------------
:updateClasspath



if not exist "%CARBON_HOME%\repository\lib\org.wso2.carbon.registry.core-*.jar" goto noAntRan
goto skipnoAntRan

:noAntRan
echo You have to run ant inside the "%CARBON_HOME%\bin" before running the checkin client.
echo Or you have to set CARBON_HOME environment variable manually.
goto end

:skipnoAntRan

setlocal EnableDelayedExpansion
set CARBON_CLASSPATH=
FOR %%C in ("%CARBON_HOME%\lib\checkin-client*.jar") DO set CARBON_CLASSPATH=!CARBON_CLASSPATH!;"%CARBON_HOME%\lib\%%~nC%%~xC"
FOR %%C in ("%CARBON_HOME%\repository\lib\*.jar") DO set CARBON_CLASSPATH=!CARBON_CLASSPATH!;"%CARBON_HOME%\repository\lib\%%~nC%%~xC"
FOR %%C in ("%CARBON_HOME%\repository\components\extensions\*.jar") DO set CARBON_CLASSPATH=!CARBON_CLASSPATH!;"%CARBON_HOME%\repository\components\extensions\%%~nC%%~xC"
set CARBON_CLASSPATH="%JAVA_HOME%\lib\tools.jar";%CARBON_CLASSPATH%;

rem ----- Execute The Requested Command ---------------------------------------
echo Using CARBON_HOME:   %CARBON_HOME%
echo Using JAVA_HOME:    %JAVA_HOME%
set _RUNJAVA="%JAVA_HOME%\bin\java"
set CMD=RUN %*
%_RUNJAVA% %JAVA_OPTS% -cp %CARBON_CLASSPATH% -Dcarbon.home=%CARBON_HOME% org.wso2.registry.checkin.Client %CMD%
:end